os := "fcos"

scos_base_img:= "quay.io/okd/scos-content@sha256:3813e6608a999756931d3d621932af9662860e71a552b2670f9fe320bf0d3585"
fcos_base_img:= "quay.io/fedora/fedora-coreos:42.20250705.3.0"

scos_img:= "quay.io/confidential-clusters/scos"
fcos_img:= "quay.io/confidential-clusters/fcos"

fcos_os:= "fedora-coreos"
scos_os:= "rhcos"

fcos_label:= "fedora-coreos"
scos_label:= "centos-stream-coreos"

base := if os == "scos" { scos_base_img } else { fcos_base_img }
image := if os == "scos" { scos_img } else { fcos_img }
os_name := if os == "scos" { scos_os } else { fcos_os }
label := if os == "scos" { scos_label } else { fcos_label }
archive := os + ".ociarchive"
platform := "qemu"
kbc_image := "quay.io/afrosi_rh/kbs-client-image:latest"
clevis_pin_trustee_image := "quay.io/confidential-clusters/clevis-pin-trustee"

build:
    sudo podman build --no-cache --build-arg BASE={{base}} --build-arg COM_COREOS_OSNAME={{label}} --build-arg KBS_CLIENT_IMAGE={{kbc_image}} --build-arg CLEVIS_PIN_TRUSTEE_IMAGE={{clevis_pin_trustee_image}} -t {{image}} -f Containerfile .

oci-archive:
    sudo skopeo copy containers-storage:{{image}} oci-archive:{{archive}}

osbuild:
    #!/bin/bash
    set -xeuo pipefail

    SELINUX_STATUS=$(getenforce)
    
    if [ "$SELINUX_STATUS" = "Enforcing" ]; then
        sudo setenforce 0
    fi
    
    TMPDIR=$(mktemp -d)
    git clone --depth 1 https://github.com/coreos/custom-coreos-disk-images ${TMPDIR}

    sudo -E ${TMPDIR}/custom-coreos-disk-images.sh --platform {{platform}} \
        --ociarchive {{archive}} \
        --osname {{os_name}}
    rm -rf "$TMPDIR"
    sudo chown $USER:$USER {{os}}-{{platform}}.x86_64.*

    if [ "$SELINUX_STATUS" = "Enforcing" ]; then
        sudo setenforce 1
    fi
